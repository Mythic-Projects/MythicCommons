plugins {
    id 'idea'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'org.mythicprojects.commons'
    version = '0.21.1-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    compileJava.options.encoding = 'UTF-8'
}

subprojects {
    repositories {
        mavenCentral()

        // Mythic Projects
        maven { url = "https://repo.mythicprojects.org/releases" }
        maven { url = "https://repo.mythicprojects.org/snapshots" }

        // Paper
        maven { url = "https://repo.papermc.io/repository/maven-public" }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    publishing {
        publications {
            libraries(MavenPublication) {
                artifactId = project.name.toLowerCase()
                from components.java

                pom {
                    url = "https://github.com/Mythic-Projects/MythicCommons"
                    name = project.name
                    description = "MythicCommons is general purpose library for Java and Minecraft projects."

                    developers {
                        developer {
                            id = "peridot"
                            name = "Peridot"
                            email = "peridot491@pm.me"
                        }
                    }

                    scm {
                        url = "https://github.com/Mythic-Projects/MythicCommons.git"
                        connection = "git@github.com:Mythic-Projects/MythicCommons.git"
                        developerConnection = "git@github.com:Mythic-Projects/MythicCommons.git"
                    }

                    licenses {
                        license {
                            name = "The MIT License"
                            url = "http://www.opensource.org/licenses/MIT"
                            distribution = 'repo'
                        }
                    }
                }

                // Add external repositories to published artifacts
                // ~ btw: pls don't touch this
                pom.withXml {
                    def repositories = asNode().appendNode('repositories')

                    project.getRepositories().findAll { repo ->
                        if (repo.getUrl().toString().startsWith('http')) {
                            def repository = repositories.appendNode('repository')
                            repository.appendNode('id', repo.getUrl().toString().replace("https://", "").replace("/", "-").replace(".", "-").trim())
                            repository.appendNode('url', repo.getUrl().toString())
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "mythicprojects-repo"
                url = "https://repo.mythicprojects.org/${version.toString().endsWith('SNAPSHOT') ? 'snapshots' : 'releases'}"

                credentials {
                    username = System.getenv("MAVEN_NAME")
                    password = System.getenv("MAVEN_TOKEN")
                }
            }
        }
    }
}

idea {
    project {
        jdkName = "21"
    }
}